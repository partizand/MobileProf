
Функция GetData(guid, categorynum, category, questionnum, question, pic, marked, categorymarked, answers)
	Ссылка = Справочники.ВопросыИОтветы.ПолучитьСсылку(Новый УникальныйИдентификатор(guid));
	Если НЕ ЗначениеЗаполнено(Ссылка.ВерсияДанных) Тогда
		Возврат "error";
	КонецЕсли;
	
	Запрос = Новый Запрос;
		
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВопросыИОтветы.Владелец.Наименование КАК Раздел,
	               |	ВопросыИОтветы.Владелец.НомерРаздела КАК НомерРаздела,
	               |	ВопросыИОтветы.Владелец.ПометкаУдаления КАК РазделПометкаУдаления,
	               |	ВопросыИОтветы.НомерВопроса,
	               |	ВопросыИОтветы.Вопрос,
	               |	ВопросыИОтветы.ПометкаУдаления,
	               |	ВопросыИОтветы.ХранилищеКартинки
	               |ИЗ
	               |	Справочник.ВопросыИОтветы КАК ВопросыИОтветы
	               |ГДЕ
	               |	ВопросыИОтветы.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВопросыИОтветыОтветы.НомерОтвета,
	               |	ВопросыИОтветыОтветы.Ответ,
	               |	ВопросыИОтветыОтветы.ЭтоПравильный
	               |ИЗ
	               |	Справочник.ВопросыИОтветы.Ответы КАК ВопросыИОтветыОтветы
	               |ГДЕ
	               |	ВопросыИОтветыОтветы.Ссылка = &Ссылка";
	Результаты = Запрос.ВыполнитьПакет();
	Выборка1 = Результаты[0].Выбрать();
	Выборка2 = Результаты[1].Выбрать();
	Выборка1.Следующий();
	
	categorynum = Выборка1.НомерРаздела;
	category = Выборка1.Раздел;
	questionnum = Выборка1.НомерВопроса;
	question = Выборка1.Вопрос;
	
	marked = ?(Выборка1.ПометкаУдаления, 1, 0);
	
	categorymarked = ?(Выборка1.РазделПометкаУдаления, 1, 0);
	
	pic = Base64Строка(Выборка1.ХранилищеКартинки.Получить());
	
	Рез = "";
	
	Пока Выборка2.Следующий() Цикл
		Рез = Рез + ?(ПустаяСтрока(Рез), "", "<tr>") + СтрЗаменить(Строка(Выборка2.НомерОтвета), Символы.НПП, "") + "<td>" + Выборка2.Ответ + "<td>" + ?(Выборка2.ЭтоПравильный, "1", "0");		
	КонецЦикла;
	
	answers = Рез;
	
	Возврат "";
КонецФункции

Функция GetDataList(list)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВопросыИОтветы.Ссылка
	               |ИЗ
	               |	Справочник.ВопросыИОтветы КАК ВопросыИОтветы";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Рез = "";
	Пока Выборка.Следующий() Цикл
		Рез = Рез + ?(ПустаяСтрока(Рез), "", "<tr>") + Выборка.Ссылка.УникальныйИдентификатор();
	КонецЦикла;
	list = Рез;
	
	Возврат "";
КонецФункции
